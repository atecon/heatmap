set verbose off
clear
include heatmap.gfn
set seed 20111158


### random matrix
n = 32
A = conv2d(ones(6,6), mnormal(n, n))

# default heatmap plot
heatmap(A)
# Use alias function for heatmap
heatmap_plot(A)
bundle opts = _(title="Matrix A with contour lines",
                titlefs=18, quiet=1)
heatmap_plot(A, opts)


# Contour plot with custom options
bundle opts = _(title="Matrix A with contour lines",
                titlefs=18, quiet=1, clevels=4)
contour_plot(A, opts)

# Now as heatmap
opts.title="Matrix A with heatmap"
heatmap_plot(A, opts)


### function plot
bundle opts = _(title="binomial coefficients",
                xlabel="n", ylabel="k")
heatmap_func("bincoeff", 50, 10, 16, 1, 10, opts)

# contour plot with a grid
opts = opts + _(clevels=16, grid=1)
heatmap_func("bincoeff", 50, 10, 16, 1, 10, opts)


# Work with time-series - list of series must be passed (each plotted over time)
open denmark.gdt --quiet
## list of series
list L = diff(dataset)
heatmap_plot(L, _(title="1st differences",
                  titlefs=12))

# Work with panel data - single series only can be passed
# Values for each unit are plotted over time
open grunfeld.gdt --quiet
series y = ldiff(invest)
heatmap_plot(y, _(title="1st differences",
                  titlefs=12))
## Add markers for firms
markers --from-series=firmname
heatmap_plot(y, _(title="1st differences",
                  titlefs=12,
                  ticfs=8))

# pm3d plot
open denmark.gdt --quiet
## matrix use
matrix M = mnormal(30,15)
pm3d_plot(M)

# rotate view
bundle opts = _(with_contour=TRUE,
                grid=TRUE,
                view_x_axis=50, view_y_axis=35)
pm3d_plot(M, opts)

## list use
list L = diff(LRM) IBO IDE
pm3d_plot(L)
