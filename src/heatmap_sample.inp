set verbose off
clear
include heatmap.gfn
set seed 20111158


### random matrix
n = 32
A = conv2d(ones(6,6), mnormal(n, n))
heatmap(A, _(dest="1_heatmap.png"))             # default heatmap plot
# Use alias function for heatmap
heatmap_plot(A, _(dest="1_heatmap_plot.png"))

# Contour plot with custom options
bundle opts = _(title="Matrix A with contour lines",
                titlefs=18, quiet=1, clevels=4, dest="2_contour.png")
contour_plot(A, opts)
# Now as heatmap
opts.dest="2_heatmap.png"
opts.title="Matrix A with heatmap"
heatmap_plot(A, opts)

### function plot
bundle opts = _(title="binomial coefficients",
                xlabel="n", ylabel="k", dest="bincoeff.png")
heatmap_func("bincoeff", 50, 10, 16, 1, 10, opts)

# contour plot with a grid
opts = opts + _(clevels=16, grid=1)
opts.dest = "bincoeff_contour_with_grid.png"
heatmap_func("bincoeff", 50, 10, 16, 1, 10, opts)


# Work with time-series
open denmark.gdt --quiet
## Single series case
list X = LRM
heatmap_plot(X, _(dest="heatmap_ts_single.png", title="Denmark unemployment rates",
                    titlefs=12))
## list of series
list L = diff(dataset)
heatmap_plot(L, _(dest="heatmap_ts_list.png", title="Denmark unemployment rates",
                  titlefs=12))


# Work with panel data
open grunfeld.gdt --quiet
series y = ldiff(invest)
heatmap_plot(y, _(dest="heatmap_panel.png"))
## Add markers for firms
markers --from-series=firmname
heatmap_plot(y, _(dest="heatmap_panel_w_markers.png"))

